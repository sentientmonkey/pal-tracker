import org.flywaydb.gradle.task.FlywayMigrateTask

plugins {
    id "java"
    id "org.springframework.boot" version "1.5.4.RELEASE"
    id 'net.ltgt.apt' version '0.10'
    id "org.flywaydb.flyway" version "4.2.0"
}

repositories {
    mavenCentral()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")

    testCompile("org.springframework.boot:spring-boot-starter-test")

    compileOnly 'org.projectlombok:lombok:1.18.2'
    compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.1")
    compile("org.springframework.boot:spring-boot-starter-jdbc:2.0.1.RELEASE")
    compile("mysql:mysql-connector-java:8.0.11")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.boot:spring-boot-starter-security")

    apt 'org.projectlombok:lombok:1.18.2'
}


def developmentDbUrl = "jdbc:mysql://localhost:3306/tracker_dev?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
bootRun.environment([
        "WELCOME_MESSAGE": "hello",
        "SPRING_DATASOURCE_URL": developmentDbUrl,
        "MANAGEMENT_SECURITY_ENABLED": false,
        "HTTPS_DISABLED": true,
])

def testDbUrl = "jdbc:mysql://localhost:3306/tracker_test?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
test.environment([
        "WELCOME_MESSAGE": "Hello from test",
        "SPRING_DATASOURCE_URL": testDbUrl,
        "MANAGEMENT_SECURITY_ENABLED": false,
        "HTTPS_DISABLED": true,
])

flyway {
    url = developmentDbUrl
    user = "tracker"
    password = ""
    locations = ["filesystem:databases/tracker/migrations"]
}

task testMigrate(type: FlywayMigrateTask) {
    url = testDbUrl
}

springBoot {
    buildInfo()
}